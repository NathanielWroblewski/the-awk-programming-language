# exercise 3-2
#   Add the more general regular expression for a number.
#   How does it effect the running time?
#     > $ time awk -f exercise-3-2 numbers.dat
#     >    real  0m0.003s
#     >    user  0m0.001s
#     >    sys   0m0.002s
#     > $ time awk -f exercise-3-1 numbers.dat
#     >    real  0m0.003s
#     >    user  0m0.001s
#     >    sys   0m0.002s
#     > Negligible effect on runtime.
#
# sum - print sums of numeric columns
#   input:  rows of integers and strings
#   output: sums of numeric columns
#     assumes every line has same layout

NR == 1 {
          nfld = NF
          for (i = 1; i <= NF; i++) {
            numcol[i] = isnum($i)
          }
        }

NF > 0  {
          for (i = 1; i <= NF; i++)
            if (numcol[i])
              sum[i] += $i
        }

END     {
          for (i = 1; i <= nfld; i++) {
            if (numcol[i])
              printf("%g", sum[i])
            else
              printf("--")
            printf(i < nfld ? "\t" : "\n")
          }
        }

function isnum(n) { return n ~ /^[+-]?[[:digit:]]+$/ }
