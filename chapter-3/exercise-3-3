# exercise 3-3
#   What is the effect of removing the test of numcol in the second for loop?
#     > Will coerce non-numeric columns into a zero and sum them.
#     > Negligible effect on runtime.
#
# sum - print sums of numeric columns
#   input:  rows of integers and strings
#   output: sums of numeric columns
#     assumes every line has same layout

NR == 1 {
          nfld = NF
          for (i = 1; i <= NF; i++) {
            numcol[i] = isnum($i)
          }
        }

NF > 0  {
          for (i = 1; i <= NF; i++)
            sum[i] += $i
        }

END     {
          for (i = 1; i <= nfld; i++) {
            if (numcol[i])
              printf("%g", sum[i])
            else
              printf("--")
            printf(i < nfld ? "\t" : "\n")
          }
        }

function isnum(n) { return n ~ /^[+-]?[[:digit:]]+$/ }
